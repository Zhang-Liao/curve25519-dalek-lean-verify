/-
Copyright (c) 2025 Beneficial AI Foundation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Dablander, Hoang Le Truong
-/
import Curve25519Dalek.Funs
import Curve25519Dalek.Defs
import Curve25519Dalek.Aux
import Curve25519Dalek.Specs.Backend.Serial.U64.Field.FieldElement51.Reduce
import Curve25519Dalek.Specs.Backend.Serial.U64.Field.FieldElement51.ToBytes

set_option linter.style.longLine false
set_option linter.style.setOption false
set_option maxHeartbeats 10000000000000

/-!
# Spec Theorem for `FieldElement51::is_zero`

Specification and proof for `FieldElement51::is_zero`.

This function checks whether a field element is zero.

**Source**: curve25519-dalek/src/field.rs
-/

open Aeneas.Std Result

namespace curve25519_dalek.field.FieldElement51

/-!
Natural language description:

- Checks whether a field element is zero in ùîΩ_p, p = 2^255 - 19.
- Field element is in radix 2^51 with five u64 limbs.
- Returns a `subtle.Choice` (0 = false, 1 = true).

Spec:

- Function succeeds (no panic).
- For any field element `r`, result `c` satisfies
  `c.val = 1 ‚Üî Field51_as_Nat(r) % p = 0`.
-/

/-- Spec and proof concerning `FieldElement51.is_zero`. -/
lemma Choice.val_eq_one_iff (c : subtle.Choice) :
  c.val = 1#u8 ‚Üî c = Choice.one := by
  cases c with
  | mk val valid =>
    simp [Choice.one]

/-- Arrays are equal if their slices are equal. -/
lemma array_eq_of_to_slice_eq
    {Œ± : Type} {n : Usize}
    {h1 h2 : Array Œ± n}
    (h : h1.to_slice = h2.to_slice) :
    h1 = h2 := by
  simp [Array.to_slice] at h
  cases h1; cases h2
  simp at h
  cases h
  rfl

@[progress]
theorem is_zero_spec (r : backend.serial.u64.field.FieldElement51) :
  ‚àÉ c, is_zero r = ok c ‚àß (c.val = 1#u8 ‚Üî Field51_as_Nat r % p = 0) := by
  unfold is_zero
  progress as ‚ü®bytes, h_to_bytes‚ü©
  progress as ‚ü®s, h_bytes_slice‚ü©
  progress as ‚ü®s1, h_zero_slice‚ü©
  progress as ‚ü®result, h_ct_eq‚ü©
  constructor
  ¬∑ intro h
    have s_eq_s1 : s = s1 := h_ct_eq.mp ((Choice.val_eq_one_iff result).mp h)
    rw [h_bytes_slice, h_zero_slice] at s_eq_s1
    have heq : bytes = Array.repeat 32#usize 0#u8 :=
      array_eq_of_to_slice_eq s_eq_s1
    rw [heq, Nat.ModEq] at h_to_bytes
    rw [‚Üê h_to_bytes]
    unfold U8x32_as_Nat
    iterate 31 (rw [Finset.sum_range_succ])
    rw [Finset.sum_range_one]
    simp [Array.repeat]
  ¬∑ intro h
    rename_i h1
    rw [Nat.ModEq, h] at h_to_bytes
    have h_bytes_zero : U8x32_as_Nat bytes = 0 := by
      have h2 := Nat.mod_eq_of_lt h1
      rw [h2] at h_to_bytes
      exact h_to_bytes
    have bytes_eq : bytes = Array.repeat 32#usize 0#u8 := by
      unfold U8x32_as_Nat at h_bytes_zero
      simp_all
      apply Subtype.ext
      apply List.ext_getElem
      repeat simp
      intro i hi _
      have hi_val := h_bytes_zero i hi
      interval_cases i
      all_goals simp [Array.repeat, List.replicate]; scalar_tac
    have s_eq_s1 : s = s1 := by
      rw [h_bytes_slice, h_zero_slice, bytes_eq]
    rw [‚Üê h_ct_eq] at s_eq_s1
    simp [s_eq_s1, Choice.one]

end curve25519_dalek.field.FieldElement51
